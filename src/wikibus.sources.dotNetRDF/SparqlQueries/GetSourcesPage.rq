prefix hydra: <http://www.w3.org/ns/hydra/core#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix wbo: <http://wikibus.org/ontology#>

construct
{
  @collection 
    a hydra:Collection ;
    hydra:member ?member ;
    hydra:totalItems ?count.

  ?member dcterms:title ?title ;
          wbo:hasImage ?hasImage .
}
where
{
  optional {
    select ?member ?title ?hasImage
    {
      graph @collection
      {
        @collection hydra:member ?member .
        ?member dcterms:title ?title .                

        optional {
            ?member wbo:hasImage ?hasImage .
        }
      }
    }
    order by ?title
    limit @limit
    offset @offset
  }

  {
    select count(*) as ?count
    {
      graph ?g { @collection hydra:member ?i. }
    }
  }
}